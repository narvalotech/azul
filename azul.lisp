;; AZUL: Bluetooth test desktop application
;;
(defun prepend-to-symbol (str sym)
  (read-from-string (concatenate 'string str (string sym))))

;; Create generic backend functions from a list
(defmacro register-bt-generics (fn-name-list functions)
  (append '(progn)
          (list `(defparameter ,fn-name-list nil))
          (loop for line in functions
                collect
                (destructuring-bind (fn-name fn-param-list fn-docstring) line
                  (let ((bt-fn-name (prepend-to-symbol "BT-" fn-name)))
                    `(progn
                       (defgeneric ,bt-fn-name (backend ,@fn-param-list)
                         (:documentation ,fn-docstring))
                       (setf ,fn-name-list (append ,fn-name-list (list ',bt-fn-name)))))))
          ))

(defmacro register-bt-events (return-list input-list)
  (append '(progn)
          (list `(defparameter ,return-list nil))
          (loop for event in input-list
                collect
                (destructuring-bind (evt-name param-list docstring) event
                  `(setf ,return-list
                         (append ,return-list
                                 (list (list ',evt-name ',param-list ,docstring))))
                  ))))

;; (equal 'err (car (nth 1 (nth 1 (nth 2 *evt-list*)))))
;; (equal 'hci-error (nth 1 (nth 1 (nth 1 (nth 2 *evt-list*)))))

;; Some testing
;; (macroexpand '(register-bt-generics *fn-list*
;;   ((id-create (&key bt-addr) "Create a Bluetooth identity.")
;;    (id-reset (id &key bt-addr) "Reset an identity."))))

;; Will create generic functions based on this.
;; Also pass device instance as first param.
;; (name (param-list) docstring)
;; The order should be kept in sync with the target (C) code
;; as it will be used for the function IDs.
(register-bt-generics
 *fn-list*
 (
  ;; Identities
  (id-create (&key bt-addr) "Create an identity.")
  (id-list () "List identities.")

  ;; Advertising
  (advertise () "Start legacy advertising")

  ;; Scan
  (scan (type) "Scan for BLE devices.")
  (scan-filter-set (filter) "Set a scan filter (e.g. name, addr, rssi).")
  (scan-filter-clear () "Clear all scan filters.")

  ;; Connections
  (connect (&key bt-addr) "Connect to a BLE device.")
  (conn-param-update (&key conn-id min max latency timeout)
                     "Update connection parameters.")
  (data-length-update (&key conn-id tx-max-length tx-max-time) "Perform DLE.")
  (phy-update (&key tx-phy rx-phy s2 s8) "Perform a PHY update procedure.")
  (list-connections () "List active connections.")

  ))

(register-bt-events
 *evt-list*
 (
  ;; Scan
  (scan-device-report ((addr 'bt-addr)
                       (sid 'u8)
                       (rssi 'i8)
                       (tx-power 'i8)
                       (adv-type 'u8)
                       (adv-props 'u16)
                       (interval 'u16)
                       (primary-phy 'u8)
                       (secondary-phy 'u8)
                       (adv-data 'bt-adv-data))
                      "Scanned an advertiser.")
  (scan-timeout () "Scanning has timed out.")

  ;; Connections
  (connected ((conn 'bt-conn)
              (err 'hci-error))
             "Connection was successful.")
  (disconnected ((conn 'bt-conn)
                 (reason 'hci-error))
                "Connection was not successful.")
  (param-updated ((conn 'bt-conn)
                  (param 'bt-conn-params))
                 "Connection parameters were updated.")
  (conn-remote-info ((conn 'conn)
                     (remote-info 'remote-info))
                    "Received information about the remote device.")
  (phy-updated ((conn 'bt-conn)
                (phy-info 'phy-info))
               "PHY has changed.")
  (data-length-updated ((conn 'bt-conn)
                        (data-len-info 'data-len-info))
                       "Data length was updated.")
  (connection-info ((conn 'bt-conn)
                    (type 'u8)
                    (role 'u8)
                    (id 'u8)
                    (conn-le-info 'conn-le-info))
                   "Received information about a given connection.")
  ))

(defun get-idx-by-name (name-list name)
  ;; Get function or event index in list
  (loop for el in name-list
        counting t into idx
        when (eql el name) do (return (1- idx))))

;; (nth (get-idx-by-name *fn-list* 'bt-connect) *fn-list*)

;; Zephyr host RPC-ish backend
(defclass zephyr-host () nil)
(defparameter *backend-inst* (make-instance 'zephyr-host))

(defun serialize-command (opcode &optional data)
  (concatenate 'vector
               ;; Magic value for commands
               #(#xab)
               (vector (get-idx-by-name *fn-list* opcode))
               data))

(defun print-hex (vec)
  (format nil "窿磲ъ轶灬礅溽ㄡㄦ矧磲铋铂О岍鲥悌┅ㄤ彐躅溟箴狒汨箦蜷犰ㄥ钽镤邃换韵南箦钿秭弪蝈犰箦蜷犰ㄦ矧磲⒃睾狺ア痱轭舡桢孱泔溴洎┅ㄤ彐沆狍怍徜潋īè徜潋洪铋翎蜱横滗洪铋翩矧Ж鞍鞍鞍鞍鞍鞍┅豉疱洪铋翎蜱呼疱洪铋翩矧候犷滹愆┅ㄤ彐礤翳镤珏舡箝è豉疱ㄥ耢р舡徜潋┅珐换清扉篝镦箪雉ㄤ彐躅珏舡箪雉钺礤镡赍泗磲疸狎＇筲盹鸷箪雉溴骈铋糸镱钺礤筲盹鸷沆狍蟓溟蝈泗箪雉ㄣ灬篌镦镡赍泗┅┅换ㄤ彐疳蜥礤翦翦篝轭篝磲脲轭篝犷沐р舡徜潋横滗Жｘ破ｘ排ｘ哪ｘ鞍ｘ北ｘ膊呼疱吼踱扉悌换ㄦ矧磲誉ア翦篝轭篝换鏖翳箪雉ㄡ滗豉疱翦篝轭篝换痱轭徜潋换痱轭豉疱┅换骑铒镱禊篚痧矧趔蟓屮痼镱箝铉戾扉铄ㄤ彐躅疳蝮瀛泔眄犷篝颟戾è沩蝈徜骝镯篝蜷铉篝颟┅ㄥ鲠ㄡ痧孱扉篝ㄦ轵篝沩洎忉汶孱洵轭篝蝈篝沩洎┅┅ㄤ彐疳蜥礤翦翦篝扉篝Ж怍殇泸遽翦衡舡徜潋Жò北膊吵创档蜥钿镯┅痱轭翦篝扉篝疳蝮瀛泔眄犷ㄦ矧磲铋英翦篝扉篝┅耢厚蹰汶祜徜Ⅴ箫汶弭ㄤ彐躅泸遽翦箦蝣弪犷洵扉篝孱痫螋ㄦ矧磲⑾疱铋铉忉汶孱镱痫螋岙ア痫螋躞镢脲艉鏖翳箫汶弭扉篝孱弪箦蝣弪箫汶弭⒈卜爱爱雹痫螋候艴箦徜潋弩舂躞镢脲艉麽轸骘颦轭瘐箦蝣弪箫汶弭躞镢脲艉鏖翳泔铑邈翦洵箫汶弭箦蝣弪泔铑躞镢脲艉箫汶弭徙沐痿箦蝣弪箫汶弭┅戾è箦蝣弪篝蝈犴躞镢脲艉箫汶弭篝蝈犴箦蝣弪泔铑┅ㄦ矧磲⒚扉孱泔铑邈翦濑アㄨ犷潇弪汜箦祜镳戾è扉铄蝈徜扉铄箦蝣弪篝蝈犴┅ㄦ矧磲⒁睾窿ア扉铄疳蝮瀛泔眄犷扉铄┅ㄥ钿镦骈戾ㄣㄦ矧磲⒚扉孱溟筱镱铄泗邃屮轸轭忉汶孱濑ア鲠祯弩悌┅┅换冕躞铄翥狒麸翎祀麸轸换钽辈樊爱爱床岸换ㄣ蝈狒瀛箦蝣弪犷洵扉篝孱床岸供ㄤ彐躅泸遽翦沆殄铘痫螋躞镢脲艉鏖翳沆殄铘箫汶弭箫汶弭篝蝈犴⒈卜爱爱雹痫螋哄戾礤铘豉疱с栳蜥泗弪祜镳蝈疱狒滹痱镧ㄦ矧磲篝蝈犴誉ア翦篝扉篝ㄦ矧沐秕麴豸篝蝈犴┅┅换ㄣ蝈狒瀛沆殄铘床岸供换麻钺蝙疳汶轭换义趱蝾怡翦狎蜥鲥泗矧ㄤ彐珏铄蜷孱泔溴忾钺蝙镡赍泗ê滹沲礤铘狒轱⑴钽镤玳鲥镡赍泗轭麸忾钺蝙ê礤翳镤鲠祯濠ㄦ矧磲铋⑽孱泔溴礤翳镤溴骈铄骘玳鲥豉疱岙豉疱镦鲠祯濠┅ㄤ彐珏铄蜷溴泔溴忾钺蝙蜥豉疱ê滹沲礤铘狒轱⒛邈镤蜥忾钺蝙篝蝈犴轭麸犷镡赍泗镦圄疱啖ê礤翳镤蜥豉疱ㄦ矧磲铋⑽溴泔溴礤翳镤溴骈铄骘玳鲥豉疱岙豉疱┅ㄤ彐珏铄蜷珏舡箝豉疱ê滹沲礤铘狒轱⒁弭躜箝轭怡翦镦玳鲥豉疱ê礤翳镤豉疱ㄦ矧磲铋⑽箝礤翳镤溴骈铄骘玳鲥豉疱岙豉疱┅ㄤ彐躅孱泔溴戾ㄢ翦鲠祯濠祜镳骘怡翦骝镯麸ū怡翦螬泔祆邈熹ㄢ翦í怡翦┅鲠祯濠┅ㄤ彐躅溴泔溴戾ㄢ翦蜥鳗戾è蜥ㄣ镥蜚蜥ъ轶舂┅祜镳骘骝镯麸ū怡翦螬篚眄轭ㄡ箬铘蜥鳗í俯┅┅换俞礤狍溴泔溴戾怩蝈盹鲥溴泔溴疳螋骝镯疳篌邃鲥泗矧ㄤ彐磲泸瘐祆戾ㄢ翦蜥鳗啜戾è蝈篚祠┅痱镧箦翩蝈篚祠ㄤ邈镤瀛戾怡翦蜥鳗箦翩蜥篚怏羼蜥怡翦螬蝈篚祠┅换ㄤ彐疳蜥礤翦翦篝（订换磲泸镥疳钿Ж瘐祆戾翦篝┅换瘐祆戾翦篝换痱轭翦篝换拈趑怩骘溴泔溴忾钺蝙ㄤ彐磲泸瘐祆忾钺蝙蜥豉疱啜戾è蝈篚祠┅痱镧箦翩蝈篚祠ㄤ邈镤瀛忾钺蝙蜥豉疱┅箦翩蜥篚怏羼蜥ㄧ弭箝豉疱┅蝈篚祠┅换予秕熹殓铒蝈犷翳轭徭翦蝈聃轵邃怡翦换ㄤ邈镤瀛戾Ж北穿换ㄤ邈镤瀛戾（北订ㄤ彐躅扉篝蟓决邈麸é蝈篝扉篝螬ㄣ镥蜚祜镳骘轭扉篝钽镱飑鲥泗矧┅ㄤ彐躅鲥泗矧眷轶鲥泗矧ㄣ镥蜚鲥泗矧ъ轶舂换清铄蜥翦孱泔溴忾钺蝙礤翳镤廖泔铞孱殄钽骖麸磲脲翳轭沆狍箦ㄤ彐磲泸珏铄蜥翦孱泔溴忾钺蝙ㄩ铘戾è骖犴蝈徜骝镯篝蜷铉ㄣ镱汜翦钺翦篝蜷铉㈨犭瀛篝蜷铉轭舂┅┅啜痱镧ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗轭舂扉篝蟓决邈麸ㄥ钽镤瀛戾箪雉鲠祯镡赍泗р翦螬箪雉鲠祯镡赍泗鲠祯濠┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢К轭舂┅ㄤ邈镤瀛戾ㄧ弭箝К轭舂蜥鳗ㄤ彐礤翳镤珏舡箝è豉疱ㄥ耢К轭舂┅箪雉鲠祯磲脲轭篝犷沐К轭舂р翦螬ㄤ彐躅骖犴鲠祯濠磲脲轭篝犷沐К轭忽犰蹂鲠祯濠┅┅ㄤ彐躅磲脲醺鲠祯濠磲脲轭篝犷沐醺忽犰蹂鲠祯濠ㄤ彐沆狍醺īè鲠祯洪铋翎蜱忽犰蹂洪铋翩矧癌ㄢ翦洪铋翎蜱衡翦洪铋翩矧暴┅ㄧ孱弪狒瀛孱泔溴忾钺蝙醺ㄥ钽镤瀛忾钺蝙磲脲醺辈畅ㄤ彐沆狍楦īè鲠祯洪铋翎蜱忽犰蹂洪铋翩矧癌ㄢ翦洪铋翎蜱衡翦洪铋翩矧暴┅ㄧ孱弪狒瀛孱泔溴忾钺蝙楦ㄥ钽镤瀛忾钺蝙磲脲楦卑┅ㄤ彐沆狍醣īè鲠祯洪铋翎蜱忽犰蹂洪铋翩矧癌ㄢ翦洪铋翎蜱衡翦洪铋翩矧博┅ㄧ孱弪狒瀛孱泔溴忾钺蝙醣订ㄥ钽镤瀛忾钺蝙磲脲醣ｘ骀徕┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗泔铙┅扉篝蟓决邈麸镡赍泗┅ㄤ彐疳蜥礤翦怍徜潋豉疱螵Ж吼踱扉候犷滹畅ㄤ彐躅珏舡孱蹴钺礤铛礅弪孱蹴祜镳骘屐轭ㄣ潋孱蹴泔躅糸铉轭麸麒孱ㄥ聃犰屐铛礅弪滹蝈趱蝾铘ō暴孱蹴┅骈钺祆蝈趱蝾铋飑┅换ㄧ弭孱蹴钺礤怍徜潋豉疱螵ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗怍徜潋┅鏖翳箪雉ㄡ滗豉疱镡赍泗ㄣ镱汜翦钺翦鲥泗矧扉篝蟓决邈麸徜潋ㄥ钽镤瀛戾ㄧ弭怍徜潋豉疱螵豉疱┅┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢р舡徜潋┅箦翩蜥鲥泗矧眷轶蜥鳗磲脲轭篝犷沐豉疱横滗篚怏羼蜥订呼疱ㄧ弭孱蹴钺礤ㄤ邈镤瀛戾篚怏羼蜥订怍徜潋豉疱螵┅换ㄤ彐疳蜥礤翦翦篝徜潋换ㄤ邈镤瀛忾钺蝙换ㄥ钽镤瀛忾钺蝙磲脲轭篝犷沐р舡徜潋换横滗Жｘ破ｘ排ｘ哪ｘ鞍ｘ北ｘ膊换呼疱吼踱扉悌р舡徜潋┅换韵南箦弪蝻钺礤矧泔溴狨麸磲糸汜祆ㄤ彐沆狍桡榄弪蝻īè泔溴洪铋翎蜱恒镤洪铋翩矧癌钺礤洪铋翎蜱侯犴洪铋翩矧┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗桡榄弪蝻颟扉篝蟓决邈麸ㄥ钽镤瀛戾箪雉鲠祯镡赍泗с镤濠┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢ц汩弪蝻颟┅磲脲轭篝犷沐豉疱恒镤ㄤ邈镤瀛戾蜥鳗┅换ㄤ彐疳蜥礤翦翦篝轭篝换ㄤ邈镤瀛忾钺蝙ㄥ钽镤瀛忾钺蝙磲脲轭篝犷沐ц汩弪蝻恒镤博ц汩弪蝻颟ㄤ彐沆狍怍徜霏溽翎īè戾洪铋翎蜱红孱洪铋翩矧癌ㄤ狒洪铋翎蜱轰狒洪铋翩矧癌┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗怍徜霏溽翎┅扉篝蟓决邈麸ㄥ钽镤瀛戾箪雉鲠祯镡赍泗ъ孱┅箪雉鲠祯镡赍泗т狒岍┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢р舡徜霏溽翎┅戾è戾ㄤ邈镤瀛戾蜥鳗┅磲脲轭篝犷沐豉疱红孱戾轰狒篚怏羼蜥ǐ戾瞟┅┅换ㄤ彐疳蜥礤翦翦篝轭篝磲脲轭篝犷沐р舡徜霏溽翎红孱倍蛋轰狒Жｘ破┅换ㄤ彐疳蜥礤翦翦篝轭篝换ㄤ邈镤瀛忾钺蝙换ㄥ钽镤瀛忾钺蝙换磲脲轭篝犷沐р舡徜霏溽翎红孱轰狒Жｘ破┅换р舡徜霏溽翎┅换射遽骘狨麸珏铄蜥糸铉翳孱泔溴蚝换黹玷篝殪铄邃沲篝镯镱弩骘豉疱麒殂滹瞌孱泔溴犰翳彘换箪雉蟋瀹绠桡榄弪蝻虍换祜镳骘箪雉轭ㄧ弭箪雉钺礤翦篝轭篝换钽镱鲥泗矧眷轶换ㄥ钽镤瀛忾钺蝙箪雉鲠祯翦篝轭篝箪雉┅┅ㄤ彐沆狍怍泔铑īè痿洪铋翎蜱吼趄洪铋翩矧癌ㄩ澍洪铋翎蜱洪澍洪铋翩矧癌┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗怍泔铑┅扉篝蟓决邈麸ㄥ钽镤瀛戾箪雉鲠祯镡赍泗ю趄┅ㄥ钽镤瀛戾箪雉鲠祯镡赍泗ч澍┅┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢р舡泔铑┅磲脲轭篝犷沐豉疱吼趄瘐祆戾蜥鳗洪澍瘐祆戾蜥鳗┅换ㄤ彐疳蜥礤翦翦篝轭篝换ㄤ邈镤瀛忾钺蝙ㄥ钽镤瀛忾钺蝙磲脲轭篝犷沐р舡泔铑吼趄ｘ舶鞍骀徕洪澍暴р舡泔铑┅ㄤ彐沆狍怍泔铑疳蜥īè轭翦蝣犰黹洪铋翎蜱洪铘弪鲠飙黹洪铋翩矧癌ㄩ铘弪鲠飙磲洪铋翎蜱洪铘弪鲠飙磲洪铋翩矧癌灬翦钽洪铋翎蜱红狒孱泫洪铋翩矧癌糸礤秕洪铋翎蜱呼轫屣豸洪铋翩矧癌┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗怍泔铑疳蜥愆鏖翳箪雉ㄩ铘弪鲠飙黹轭翦蝣犰磲灬翦钽糸礤秕舂镡赍泗扉篝蟓决邈麸ㄥ钽镤瀛戾轭翦蝣犰黹瞟ㄥ钽镤瀛戾轭翦蝣犰磲ㄥ钽镤瀛戾灬翦钽ㄥ钽镤瀛戾糸礤秕舂┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢р舡泔铑疳蜥愆┅磲脲轭篝犷沐豉疱洪铘弪鲠飙黹瘐祆戾蜥鳗洪铘弪鲠飙磲瘐祆戾蜥鳗红狒孱泫瘐祆戾蜥鳗呼轫屣豸瘐祆戾蜥鳗┅ㄤ彐礤翳镤珏舡箝è豉疱ㄥ耢р舡泔铑疳蜥愆┅俯换ㄤ彐疳蜥礤翦翦篝轭篝换ㄤ邈镤瀛忾钺蝙换ㄥ钽镤瀛忾钺蝙磲脲轭篝犷沐р舡泔铑疳蜥换洪铘弪鲠飙黹舶换洪铘弪鲠飙磲嘲鞍换红狒孱泫换呼轫屣豸窗鞍┅р舡泔铑疳蜥愆ㄤ彐沆狍怍蝈盹翦轭骘īè豉疱洪铋翎蜱呼疱洪铋翩矧癌鲥蝮轱洪铋翎蜱忽弪箝镱洪铋翩矧癌磲铛驷泗躜弪洪铋翎蜱喉犷蹑徙趱蝈洪铋翩矧癌篚怫弪箝镱洪铋翎蜱后踱鲥蝮轱洪铋翩矧癌ㄦ遽趱蝈洪铋翎蜱烘遽趱蝈洪铋翩矧癌┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗怍蝈盹翦轭骘┅鏖翳箪雉豉疱鲥蝮轱磲铛驷泗躜弪篚怫弪箝镱驽狒躜弩镡赍泗扉篝蟓决邈麸ㄥ钽镤瀛戾豉疱ㄥ钽镤瀛戾鲥蝮轱瞟ㄥ钽镤瀛戾磲铛驷泗躜弪ㄥ钽镤瀛戾篚怫弪箝镱ㄥ钽镤瀛戾驽狒躜弩┅┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢р舡蝈盹翦轭骘┅磲脲轭篝犷沐豉疱呼疱瘐祆戾蜥鳗忽弪箝镱瘐祆戾蜥鳗喉犷蹑徙趱蝈瘐祆戾蜥鳗后踱鲥蝮轱瘐祆戾蜥鳗烘遽趱蝈瘐祆戾蜥鳗┅换ㄤ彐疳蜥礤翦翦篝轭篝换ㄤ邈镤瀛忾钺蝙ㄥ钽镤瀛忾钺蝙磲脲轭篝犷沐р舡蝈盹翦轭骘换呼疱ｘ婆换忽弪箝镱渤换喉犷蹑徙趱蝈ｘ徕沅换后踱鲥蝮轱ｘ辈炒换烘遽趱蝈畅р舡蝈盹翦轭骘┅ㄤ彐沆狍怍痂轭骘īè豇洪铋翎蜱呼洪铋翩矧癌蝤洪铋翎蜱候洪铋翩矧癌┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗怍痂轭骘┅鏖翳箪雉豇蝤镡赍泗扉篝蟓决邈麸ㄥ钽镤瀛戾豇ㄥ钽镤瀛戾蝤┅┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢р舡痂轭骘┅磲脲轭篝犷沐豉疱呼瘐祆戾蜥鳗候瘐祆戾蜥鳗┅换ㄤ彐疳蜥礤翦翦篝轭篝换ㄤ邈镤瀛忾钺蝙ㄥ钽镤瀛忾钺蝙磲脲轭篝犷沐р舡痂轭骘换呼换候暴р舡痂轭骘┅ㄤ彐沆狍怍溽翎戾瞽轭骘īè豇磲戾洪铋翎蜱呼磲戾洪铋翩矧癌豇磲糸礤洪铋翎蜱呼磲糸礤洪铋翩矧癌蝤磲戾洪铋翎蜱候磲戾洪铋翩矧癌蝤磲糸礤洪铋翎蜱候磲糸礤洪铋翩矧癌┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗怍溽翎戾瞽轭骘┅鏖翳箪雉豇磲戾豇磲糸礤蝤磲戾蝤磲糸礤镡赍泗扉篝蟓决邈麸ㄥ钽镤瀛戾豇磲戾瞟ㄥ钽镤瀛戾豇磲糸礤ㄥ钽镤瀛戾蝤磲戾瞟ㄥ钽镤瀛戾蝤磲糸礤┅┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢р舡溽翎戾瞽轭骘┅磲脲轭篝犷沐豉疱呼磲戾瘐祆戾蜥鳗呼磲糸礤瘐祆戾蜥鳗候磲戾瘐祆戾蜥鳗候磲糸礤瘐祆戾蜥鳗┅换ㄤ彐疳蜥礤翦翦篝轭篝换ㄤ邈镤瀛忾钺蝙ㄥ钽镤瀛忾钺蝙磲脲轭篝犷沐р舡溽翎戾瞽轭骘换呼磲戾捕换候磲糸礤ｘ驽彐┅р舡溽翎戾瞽轭骘┅ㄤ彐沆狍怍泔铑戾轭骘īè祜汜洪铋翎蜱红镢犰洪铋翩矧磲脲轭篝犷沐р舡徜潋┅蝈盹翦洪铋翎蜱候屙雉洪铋翩矧磲脲轭篝犷沐р舡徜潋┅祜汜飙箦趱洪铋翎蜱红镢犰箦趱洪铋翩矧磲脲轭篝犷沐р舡徜潋┅蝈盹翦箦趱洪铋翎蜱候屙雉瀛箦趱洪铋翩矧磲脲轭篝犷沐р舡徜潋┅疳蜥洪铋翎蜱吼狎犴洪铋翩矧磲脲轭篝犷沐р舡泔铑疳蜥愆┅换韵南躞洪铋翩矧犷沆狍蟓镦麸狨麸珏铄蜥翦孱泔溴忾钺蝙ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗怍泔铑戾轭骘┅鏖翳箪雉祜汜蝈盹翦祜汜飙箦趱蝈盹翦箦趱疳蜥愆镡赍泗ㄣ镱汜翦钺翦鲥泗矧ㄥ钽镤瀛忾钺蝙祜汜飑ㄥ钽镤瀛忾钺蝙蝈盹翦ㄥ钽镤瀛忾钺蝙祜汜飙箦趱皓ㄥ钽镤瀛忾钺蝙蝈盹翦箦趱皓ㄥ钽镤瀛忾钺蝙疳蜥愆┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢р舡泔铑戾轭骘┅磲脲轭篝犷沐豉疱红镢犰瘐祆忾钺蝙蜥р舡徜潋候屙雉瘐祆忾钺蝙蜥р舡徜潋红镢犰箦趱瘐祆忾钺蝙蜥р舡徜潋候屙雉瀛箦趱瘐祆忾钺蝙蜥р舡徜潋吼狎犴瘐祆忾钺蝙蜥р舡泔铑疳蜥愆┅换ㄤ彐疳蜥礤翦翦篝轭篝换ㄤ邈镤瀛忾钺蝙换戾è祜汜飙徜潋换磲脲轭篝犷沐р舡徜潋换横滗Жｘ破ｘ排ｘ哪ｘ鞍ｘ北ｘ膊呼疱吼踱扉悌换蝈盹翦徜潋换磲脲轭篝犷沐р舡徜潋换横滗Жｘ徕ｘ沅ｘ彐ｘ鞍ｘ北ｘ膊呼疱吼踱扉悌换疳蜥换磲脲轭篝犷沐р舡泔铑疳蜥洪铘弪鲠飙磲卑呼轫屣豸卑┅换ㄥ钽镤瀛忾钺蝙磲脲轭篝犷沐р舡泔铑戾轭骘换红镢犰祜汜飙徜潋换候屙雉磲脲轭篝犷沐р舡徜潋换红镢犰箦趱磲脲轭篝犷沐р舡徜潋换候屙雉瀛箦趱蝈盹翦徜潋换吼狎犴疳蜥愆┅换р舡泔铑戾轭骘┅换则麸珏铄蜥翦翳孱泔溴蝮犷溴泔溴蝮狨麸磲糸汜祆换ㄤ彐躅磲脲忾瞽箪雉钺礤豉疱脲ㄩ铋癌铒孱泔溴铋飑ㄤ邈灬蝈ㄩ珙矧豉疱铒孱泔溴┅啜钺礤洪铋翎蜱痱屦孱洵麸簌礅镬⒑钺礤洪铋翩矧轭轸┅ㄤ彐躅磲脲孱泔溴箪雉镡钺礤豉疱脲ㄩ铋癌铒孱泔溴铋飑ㄤ邈灬蝈ㄩ珙矧豉疱轭轸┅ㄩ铒孱泔溴铋啜孱泔溴忾钺蝙箪雉鲠祯镡痱屦孱洵麸簌礅镬Б钺礤┅┅ㄤ彐躅磲脲溴泔溴箪雉钺礤豉疱脲ㄩ铋癌铒孱泔溴铋飑ㄤ邈灬蝈ㄩ珙矧轭轸┅ㄩ铒孱泔溴啜痱屦孱洵麸簌礅镬⒑钺礤铋飑啜痱屦孱洵麸簌礅镬⒑钺礤ㄤ邈镤瀛忾钺蝙蜥痱屦孱洵麸簌礅镬Б豉疱┅┅ㄤ彐磲泸磲脲忾钺蝙沆狍钺礤蝈篝箪雉螬啜痱镧ㄤ彐沆狍钺礤ī祜镳骘轭箪雉泔祆邈ㄡ痧禊＇磲脲忾瞽箪雉螬┅ㄤ彐礤翳镤孱泔溴忾钺蝙è镡赍泗钺礤┅ㄣ镱汜翦钺翦鲥泗矧括祜镳骘轭箪雉泔祆邈ㄡ痧禊＇磲脲孱泔溴箪雉э怅邈螬┅ㄤ彐礤翳镤溴泔溴忾钺蝙蜥豉疱ㄥ耢痱屦孱洵麸簌礅镬Б钺礤┅┅磲脲轭篝犷沐豉疱括祜镳骘轭箪雉钽镱ㄡ痧禊＇磲脲溴泔溴箪雉螬┅┅换磲泸镥疳钿换Ж磲脲忾钺蝙沆狍换桡榄弪蝻换ㄣ镤醺洪铋癌换钺礤箝眇戾狎蜥洪铋侯锃孱泔溴舂┅换磲脲忾钺蝙沆狍换桡榄弪蝻换ㄣ镤醺洪铋癌换钺礤箝眇戾狎蜥洪铋侯锃孱泔溴舂换蔑眄犷箦蜷犰辁弪换＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃＃ㄤ彐礤翳镤怍殇泸遽翦è忉汶孱屦棂颦栾篝脲怍徜潋ㄤ轶疳翥璀箦蜷犰箦蜷犰辁瀛泔眄犷р舡殇泸遽翦ㄥ钽镤瀛忾钺蝙怍徜潋┅┅换ㄢ舡殇泸遽翦忉汶孱洵轭篝换衡舡徜潋换磲脲轭篝犷沐换р舡徜潋换横滗Жｘ破ｘ排ｘ哪ｘ鞍ｘ北ｘ膊换呼疱吼踱扉悌ㄤ彐礤翳镤怍殇扉篝è忉汶孱屦棂颦栾篝┅ㄤ轶疳翥璀箦蜷犰箦蜷犰辁瀛泔眄犷р舡殇扉篝┅换ㄢ舡殇扉篝忉汶孱洵轭篝ㄤ彐礤翳镤怍筱犷è忉汶孱屦棂颦栾篝豉疱ㄤ轶疳翥璀箦蜷犰箦蜷犰辁瀛泔眄犷р舡筱犷ㄥ钽镤瀛忾钺蝙豉疱┅┅换ㄢ舡筱犷忉汶孱洵轭篝磲脲醺ｘ吵┅ㄤ彐礤翳镤怍徜鲥螋轶è忉汶孱屦棂颦栾篝┅ㄤ轶疳翥璀箦蜷犰箦蜷犰辁瀛泔眄犷р舡徜鲥螋轶濠┅ㄢ舡徜鲥螋轶忉汶孱洵轭篝